/*
 * Copyright (c) 2019 Telekom Deutschland AG
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// --- FindBugs ---
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "checkstyle"

pmd {
    toolVersion = "5.8.1"
}

checkstyle {
    toolVersion = "8.1"
}
// --- Find bugs
task findbugs(type: FindBugs) {
    description "Run FindBugs"

    group = "quality"

    effort = "max"
    reportLevel = "high"

    includeFilter = new File("${project.rootDir}/quality-profiles/findbugs/filter_include.xml")
    excludeFilter = new File("${project.rootDir}/quality-profiles/findbugs/filter_exclude.xml")

    classes = files("$project.buildDir/intermediates/classes")
    classpath = files()

    source "src"
    include "**/*.java"
    exclude "**/gen/**"

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination new File("$project.buildDir/quality/findbugs/findbugs.html")
        }
    }

    ignoreFailures = false
}

// --- Pmd ---
task pmd(type: Pmd) {
    description "Run Pmd"

    group = "quality"

    ruleSetFiles = files("${project.rootDir}/quality-profiles/pmd/rules.xml")
    ruleSets = []

    source "src"
    include "**/*.java"
    exclude "**/gen/**", "**/test/**", "**/androidTest/**", "**/defaulthandlers/**", "**/defaultcontrollers/**"

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination new File("$project.buildDir/quality/pmd/pmd.html")
        }
    }

    ignoreFailures = false
}

// --- CheckStyle ---
task checkstyle(type: Checkstyle) {
    description "Run checkstyle"

    group = "quality"

    ignoreFailures = true

    source "src"
    include "**/*.java"
    exclude "**/gen/**", "**/test/**", "**/androidTest/**"

    configFile = new File("${project.rootDir}/quality-profiles/checkstyle/checkstyle.xml")
    classpath = files("$project.buildDir/intermediates/classes")

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination new File("$project.buildDir/quality/checkstyle/checkstyle.html")
        }
    }
}